plugins {
    id 'java'
    id 'maven-publish'
    id 'idea'
    id 'application'
    id 'org.beryx.runtime' version '1.12.1'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'checkstyle'
}

group = 'BIP'
version = '2.0'
description = 'bip'
mainClassName = 'Launcher'
applicationName = "Barcode Image Processor"

checkstyle {
    toolVersion = "8.40"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url = uri('https://repo1.maven.org/maven2/com/google/zxing/javase/3.4.1/javase-3.4.1.jar')
    }
}

sourceCompatibility = 15
targetCompatibility = 15
def JAVA_HOME = System.properties.find { it.key == "java.home" } .getValue()

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javafx {
    version = "15"
    modules = ['javafx.controls', 'javafx.base', 'javafx.graphics', 'javafx.fxml',
               'javafx.swing', 'javafx.media', 'javafx.web']
}

dependencies {
    implementation 'org.apache.commons:commons-imaging:1.0-alpha1'
    implementation 'com.google.zxing:javase:3.4.1'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'org.controlsfx:controlsfx:9.0.0'
    implementation 'org.openjfx:javafx:15'

    compile 'org.openjfx:javafx-base:15'
    compile 'org.openjfx:javafx-graphics:15'
    compile 'org.openjfx:javafx-controls:15'
    compile 'org.openjfx:javafx-fxml:15'
    compile 'org.openjfx:javafx-swing:15'
    compile 'org.openjfx:javafx-media:15'
    compile 'org.openjfx:javafx-web:15'
    compile 'org.controlsfx:controlsfx:9.0.0'
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.files.collect { it.name }.join(' '),
                'Main-Class': 'Launcher'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['javafx.controls', 'javafx.base', 'javafx.graphics', 'javafx.fxml',
               'javafx.swing', 'javafx.media', 'javafx.web', 'java.instrument', 'java.desktop',
               'java.scripting', 'java.logging', 'jdk.unsupported.desktop', 'java.net.http',
               'java.xml', 'jdk.jsobject', 'jdk.xml.dom', 'jdk.jfr', 'jdk.unsupported',
               'java.datatransfer']
    jpackage {
        jpackageImage {
            imageDir = file("$buildDir/jpackage/BarcodeImageProcessor")
        }
        launcher {
            runInBinDir = true
            noConsole = true
        }
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        jpackageHome = "${JAVA_HOME}"
        mainJar = "BarcodeImageProcessor-${version}.jar"
        appVersion = version
        skipInstaller = false
        installerOutputDir = file("$buildDir/jpackage-installer")
        if (currentOs.windows) {
            targetPlatform('windows-x64', "${JAVA_HOME}")
            installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut',
                                 '--app-image', "$buildDir/jpackage/BarcodeImageProcessor"]
            targetPlatformName = 'windows-x64'
            installerType = 'msi'
        } else if (currentOs.linux) {
            targetPlatform('linux-x64', "${JAVA_HOME}")
            installerOptions += ['--linux-package-name', 'hellofx','--linux-shortcut']
            targetPlatformName = 'linux-x64'
        }
    }
}

tasks.jpackageImage.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/jpackage/BarcodeImageProcessor/runtime/bin")
    }
    copy {
        from("$buildDir/jpackage/BarcodeImageProcessor/runtime/bin")
        include("zip.dll")
        into("$buildDir/jpackage/BarcodeImageProcessor")
    }
    copy {
        from("$buildDir/jpackage/BarcodeImageProcessor")
        include("/app/*")
        into("$buildDir/jpackage/BarcodeImageProcessor/runtime")
    }
}
